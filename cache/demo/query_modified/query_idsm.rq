PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX endpoint: <https://idsm.elixir-czech.cz/sparql/endpoint/>

SELECT ?CAS ?COMPOUND ?SIMILAR_COMPOUND ?SCORE WHERE {
  {
    VALUES ?CAS { "75-35-4" "71-55-6" "79-00-5" "630-20-6" "79-34-5" "106-93-4" "107-06-2" "542-75-6" "10061-01-5" "13952-84-6" "91-59-8" "553-00-4" "612-52-2" "120-23-0" "121-14-2" "93-76-5" "10519-33-2" "92-67-1" "2113-61-3" "92-93-3" "101-77-9" "81-15-2" "30560-19-1" "34256-82-1" "50594-66-6" "15972-60-8" "116-06-3" "834-12-8" "33089-61-1" "61-82-5" "84-65-1" "1332-21-4" "77536-66-4" "77536-67-5" "12172-73-5" "12001-28-4" "77536-68-6" "3337-71-1" "2302-17-2" "1912-24-9" "2642-71-9" "86-50-0" "41083-11-8" "71626-11-4" "82560-54-1" "17606-31-4" "71-43-2" "92-87-5" "36341-27-2" "85-68-7" "1820573-27-0" "65731-84-2" "82657-04-3" "485-31-4" }

    ?SYNONYM a sio:CHEMINF_000446. # CAS registry number
    ?SYNONYM sio:SIO_000300 ?CAS.
    ?SYNONYM sio:SIO_000011 ?COMPOUND.

    ?COMPOUND a vocab:Compound.

    ?ATTRIBUTE a sio:SIO_011120. # molecular structure file
    ?ATTRIBUTE sio:SIO_000011 ?COMPOUND.
    ?ATTRIBUTE sio:SIO_000300 ?MOLFILE.

    SERVICE endpoint:chebi {
      [ sachem:compound ?SIMILAR_COMPOUND;
        sachem:score ?SCORE ] sachem:similaritySearch [
        sachem:query ?MOLFILE;
        sachem:cutoff "0.7"^^xsd:double;
        sachem:similarityRadius '3'^^xsd:integer;
        sachem:aromaticityMode sachem:aromaticityDetectIfMissing;
        sachem:tautomerMode sachem:inchiTautomers
      ].
    }
  }
  UNION
  {
    SERVICE endpoint:chebi {
      ?SIMILAR_COMPOUND sachem:substructureSearch [ sachem:query '[As]' ].
    }
  }
}
ORDER BY DESC(?SCORE)