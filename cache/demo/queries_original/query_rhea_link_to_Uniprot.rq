PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX up: <http://purl.uniprot.org/core/>

SELECT ?uniprot ?mnemo ?rhea ?accession ?equation
WHERE {
  SERVICE <https://sparql.uniprot.org/sparql> {
    GRAPH <http://sparql.uniprot.org/uniprot> {
      VALUES (?rhea) { (<http://rdf.rhea-db.org/11312>) (<http://rdf.rhea-db.org/11313>) }
      ?uniprot up:reviewed true .
      ?uniprot up:mnemonic ?mnemo .
      ?uniprot up:organism ?taxid .
      ?uniprot up:annotation/up:catalyticActivity/up:catalyzedReaction ?rhea . # where ?rhea comes from query upwards
    }
  }
  ?rhea rh:accession ?accession .
  ?rhea rh:equation ?equation .
}